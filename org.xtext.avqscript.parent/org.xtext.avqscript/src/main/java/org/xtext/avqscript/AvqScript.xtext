grammar org.xtext.avqscript.AvqScript with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate avqScript "http://www.xtext.org/avqscript/AvqScript"

ScriptPackage:
	'script' 'package' name=ID
		(globalVars+=VariableDeclaration)*
		body=Body
	'end' endName=ID
;


Body:
	{Body}
	(functions+=FunctionDeclaration)*
;

FunctionDeclaration:
	'function' name=ID
	'(' (params+=VariableDeclaration (',' params += VariableDeclaration)*)? ')'
		(localVars+=VariableDeclaration)*
	'begin'
		(expressions+=FeatureCall)*
	'end' 'function'
;

FeatureCall:
	target=[FunctionDeclaration | ID]( '('args+=[VariableDeclaration](',' args+=[VariableDeclaration])*')' )? ';'
;

VariableDeclaration:
	name=ID type=Types ( ':=' default=DefaultValue )? ';'
;

DefaultValue returns ecore::EString :
	STRING |
	ID |
	INT
;

enum Types:
	TEXT="text" |
	NUMBER='number' |
	BOOLEAN='boolean'
;